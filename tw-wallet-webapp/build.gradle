buildscript {
    dependencies {
        classpath Deps.jooqCodegen
        classpath Deps.h2
    }
}

plugins {
    id "org.springframework.boot"
    id "org.flywaydb.flyway" version "6.3.2"
    id 'nu.studer.jooq' version '4.2'
    id 'java'
    id "org.sonarqube" version "3.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "tw-bc-group_tw-wallet-backend"
        property "sonar.organization", "tw-bc-group"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

dependencies {
    compile project(':tw-wallet-common')
    implementation Deps.springBootStarterAmqp
    implementation Deps.springBootStarterDataJdbc
    implementation Deps.springBootStarterWeb
    implementation Deps.springfoxSwagger2
    implementation Deps.springfoxSwaggerUI
    implementation Deps.slf4j
    implementation Deps.logbackCore
    implementation Deps.logbackClassic
    implementation Deps.jacksonCore
    implementation Deps.jacksonDatabind
    implementation Deps.jacksonAnnotations
    implementation Deps.jacksonDatatypeJdk8
    implementation Deps.jacksonDatatypeJsr310
    implementation Deps.modelmapper
    implementation Deps.modelmapperJooq
    implementation Deps.guava
    implementation Deps.postgresql
    implementation Deps.jooq
    implementation Deps.w3j
    implementation Deps.w3jContract
    implementation Deps.flyway
    implementation Deps.commonsLang3
    implementation Deps.commonsCollections4
    implementation Deps.okhttp
    implementation Deps.okhttpUrlConnection
    implementation Deps.loggingInterceptor
    implementation Deps.jwt
    implementation Deps.apacheCommonsText
    compileOnly Deps.lombok
    annotationProcessor Deps.lombok
    annotationProcessor Deps.springBootConfigurationProcessor
    testImplementation Deps.h2

    jooqRuntime Deps.postgresql
    jooqRuntime Deps.h2
}

test {
    useJUnitPlatform()
}

apply from: 'gradle/integration-test.gradle'
apply from: "gradle/enviroments.gradle"

flyway {
    url = "${environment.spring.datasource.url}"
    user = "${environment.spring.datasource.username}"
    password = "${environment.spring.datasource.password}"
    locations = "${environment.spring.flyway.locations}".split(',')
}

jooq {
    version = '3.13.1'
    edition = 'OSS'
    generateSchemaSourceOnCompilation = false
    wallet(sourceSets.main) {
        jdbc {
            driver = "${environment.spring.datasource.'driver-class-name'}"
            url = "${environment.spring.datasource.url}"
            user = "${environment.spring.datasource.username}"
            password = "${environment.spring.datasource.password}"
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = "${environment.spring.jooq.database.name}"
                inputSchema = "${environment.spring.jooq.database.inputSchema}"
                forcedTypes {
                    forcedType {
                        userType = 'com.fasterxml.jackson.databind.JsonNode'
                        binding = 'com.thoughtworks.common.config.PostgresJSONJacksonJsonNodeBinding'
                        includeExpression = '.*\\.(sub|payload).*'
                        includeTypes = '.*'
                    }
                    forcedType {
                        userType = 'com.thoughtworks.common.util.dcep.MoneyType'
                        enumConverter = true
                        includeExpression = '.*\\.money_type'
                        includeTypes = '.*'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = true
                daos = false
                pojosEqualsAndHashCode = false
                javaTimeTypes = true
            }
            target {
                packageName = 'com.thoughtworks.wallet.gen'
                directory = "src/generated/java"
            }
        }
    }
}

generateWalletJooqSchemaSource.dependsOn flywayMigrate

springBoot {
    mainClassName = 'com.thoughtworks.wallet.TWWalletWebApp'
}
